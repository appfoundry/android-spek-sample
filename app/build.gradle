apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'

def versionMajor = 0
def versionMinor = 9
def versionPatch = 0
def versionBuild = 0

def buildTime = new Date().format("dd-MM-yyyy'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion versions.compile_sdk
    buildToolsVersion versions.build_tools

    defaultConfig {
        applicationId "be.appfoundry.spekdemo"
        minSdkVersion versions.min_sdk
        targetSdkVersion versions.target_sdk

        versionCode buildVersionCode(versionMajor, versionMinor, versionPatch, versionBuild)
        versionName buildVersionName(versionMajor, versionMinor, versionPatch)

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled true
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix " - DEBUG"

            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    lintOptions {
        abortOnError true
    }
}

kapt {
    generateStubs true
}

unMock {
    keep "android.webkit.URLUtil"
    keepStartingWith "android.util."
}

dependencies {
    debugCompile(libraries.leak_canary)
    releaseCompile(libraries.leak_canary_release)

    testCompile project(':mockito1_kotlin')

    testCompile(libraries.junit,
            libraries.kotlin,
            libraries.kotlin_test,
            libraries.robolectric,
            libraries.leak_canary_release,
            libraries.spek,
            libraries.spek_runner,
            libraries.powermock_api,
            libraries.powermock_agent,
            libraries.powermock_rule,
            libraries.powermock_junit,
            libraries.junit_platform,
            libraries.assertj,
            libraries.hamcrest,
            libraries.kotlin_reflect)
    
    compile(libraries.android_appcompat,
            libraries.design_support,
            libraries.dagger,
            libraries.rxjava,
            libraries.rxandroid,
            libraries.timber,
            libraries.kotlin,
            libraries.anko,
            libraries.anko_support_v4,
            libraries.anko_appcompat,
            libraries.anko_design,
            libraries.anko_recycler_view)

    kapt(libraries.dagger_compiler)
}

def buildVersionName(versionMajor, versionMinor, versionPatch) {
    return "${versionMajor}.${versionMinor}.${versionPatch}"
}

def buildVersionCode(versionMajor, versionMinor, versionPatch, versionBuild) {
    return versionMajor * 1000 + versionMinor * 100 + versionPatch * 10 + versionBuild
}